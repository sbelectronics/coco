10 REM RESET S/SC
20 POKE &HFF7D,1:POKE &HFF7D,0
30 REM INITIALIZE VARIABLES
40 X=&HFF00: Y=&HFF7E
50 REM TURN ON S/SC SOUND
60 POKE X+1,52: POKE X+3,63: POKE X+35,60
70 REM PUT S/SC INTO DIRECT ACCESS MODE
80 GOSUB 300
90 POKE Y,&HAF
100 REM POKE A FINE TONE ON CHANNEL A
110 GOSUB 300
120 POKE Y,0
130 INPUT "ENTER A VALUE FOR A TONE ON CHANNEL A";T
140 GOSUB 300
150 POKE Y,T
160 REM CALL MIXER REGISTER
170 GOSUB 300
180 POKE Y,7
190 REM SET MIXER TO CHANNEL A TONES ONLY
200 GOSUB 300
210 POKE Y,62
220 REM CALL AMPLITUDE REGISTER FOR CHANNEL A
230 GOSUB 300
240 POKE Y,8
250 REM SET AMPLITUDE AT MAXIMUM-15
260 GOSUB 300
270 POKE Y,15
280 REM GO BACK TO CHANGE TONE ON CHANNEL A
290 GOTO 120
300 REM CHECK FOR BUSY*
310 IF (PEEK(Y) AND 128) = 0 THEN 310
320 RETURN
