10 REM RESET S/SC
20 POKE &HFF7D,1:POKE &HFF7D,0
30 REM INITIALIZE VARIABLES
40 X=&HFF00: Y=&HFF7E
50 REM TURN ON S/SC SOUND
60 POKE X+1,52: POKE X+3,63: POKE X+35,60
70 GOSUB 200
80 REM COMMAND B0 -- LOAD ALLOPHONES INTO BUFFER 0
90 POKE Y,176
100 REM READ ALLOPHONE ADDRESS STREAM TO THE S/SC
110 FOR A=1 TO 17
120 READ D
130 GOSUB 200
140 POKE Y,D
150 NEXT A
160 REM COMMAND F0 -- EXECUTE ALLOPHONE ADDRESS STREAM FROM BUFFER 0
170 GOSUB 200
180 POKE Y,240
190 END
200 REM CHECK FOR BUSY*
210 IF (PEEK(Y) AND 128)=0 THEN 210
220 RETURN
230 REM ALLOPHONE DATA ENDS WITH A PAUSE AND TERMINATOR
240 DATA 8,15,45,51,4,4,4,8,15,16,9,49,31,13,51,4,255
